%Animal 2 1240*480
title "AnimalScript test animation"
author "Ville Karavirta ville@villekaravirta.com"
{
  text "Title" "AnimalScript Parser Examples" at (0, 0) color black size 30
  text "Descr" "...for testing JSAV ASU import" offset (0, 5) from "Title" SW size 15
}
{
  setText "Descr" to "Creating a grid and setting values for cell, row, and column"
  grid "Matrix" (100, 100) lines 5 columns 5 style matrix cellWidth 50 color (255, 0, 0) elementColor (0, 0, 0) fillColor (0, 0, 0) borderColor green highlightTextColor (0, 255, 0) highlightFillColor (90, 255, 90) font Monospaced align left
  setGridValue "Matrix[0][0]" "0,0"
  setGridValue "Matrix[][2]" "2" refresh

  grid "Table" offset (50, 0) from "Matrix" NE lines 5 columns 5 style table cellWidth 50  fillColor (200, 200, 200) font Monospaced align right
  setGridValue "Table[4][]" "4" refresh

  grid "Plain" offset (50, 0) from "Table" NE lines 5 columns 5 style plain cellWidth 50  fillColor (200, 200, 200) highlightTextColor green highlightFillColor pink2 font Monospaced align center
  setGridValue "Plain[][]" "-" refresh
}
{
  setText "Descr" to "Swapping grid values"
  swapGridValues "Table[4][0]" and "Plain[3][2]"
  swapGridValues "Matrix[0][0]" and "Matrix[2][2]"
  swapGridValues "Table[][1]" and "Plain[][1]"
}
{
  setText "Descr" to "Highlighting grid cells"
  highlightGridCell "Table[4][0]" within 500 ms
  highlightGridCell "Matrix[][0]" within 500 ms
  highlightGridCell "Plain[2][]" within 500 ms
}
{
  setText "Descr" to "Highlighting grid elements"
  highlightGridElem "Table[4][4]" within 500 ms
  highlightGridElem "Matrix[][2]" within 500 ms
  highlightGridElem "Plain[4][]" within 500 ms
}
{
  setText "Descr" to "Aligning grid values"
  alignGridValue "Matrix[1][2]" right
  alignGridValue "Table[][2]" center
  alignGridValue "Plain[2][]" left
}
{
  array "array" (220, 80) color blue fillColor green elementColor (255, 255, 255) elemHighlight orange cellHighlight (128, 220, 220) length 6 "6" "1" "4" "3" "2" "5" depth 1 after 50 ticks font Monospaced size 25 bold italic
  field "field" offset (0, 0) from "array" SE color light Gray fillColor green elementColor (255, 255, 255) elemHighlight orange cellHighlight (128, 220, 220) length 6 "6" "1" "4" "3" "2" "5" depth 10 after 50 ticks font Monospaced size 25 bold italic
  square "square" (150, 150) 70 color black depth 0 filled fillColor (230, 230, 230)
  text "Texti" "Ville" at (150, 150) color (130, 130, 0) font Serif size 30 boxed
  point "point" (30, 30) color pink2
}
label "kisuli"
{
  codegroup "sourceCode" at (20, 200) color (0, 0, 0) highlightColor (255, 0, 0) contextColor (0, 0, 255) font SansSerif size 32 depth 19
  addCodeLine "public void solveTowersOfHanoi(int nrDisks) {" to "sourceCode"
  addCodeLine "lastMovedDisk = null;" to "sourceCode" indentation 2
  addCodeLine "allTowers = {TowerA, TowerB, TowerC};" to "sourceCode" indentation 2
  graph "Graph" size 5 color green bgColor (175, 170, 170) highlightColor (255, 0, 255) elemHighlightColor (255, 255, 255) nodeFontColor blue edgeFontColor (0, 0, 0) directed  weighted nodes {"A" (100, 100), "B" (300, 100), "C" (100, 300), "D" (300, 300), "E" (400, 400) } edges { (0, 1, "1") (0, 2, "2") (0, 3, "6") (1, 3, "2") (2, 3, "2") (2, 4, "1") (3, 4, "5") } showIndices depth 50
  setText of "Texti" to "Ville 21"
  setFont of "Texti" to font Serif size 50
}
{
  rectangle "box" (80, 10) (270, 55) color black depth 1
  relrectangle "box2" (80, 10) (270, 55) color red depth 1
  polyline "polyline" (90, 20) (25,25) (75,75) color (210, 120, 230) depth 1
  triangle "triangle" (190, 120) (125,125) (175,175) color (210, 120, 230) depth 1 filled fillColor green
  circle "Circle11" (237, 250) radius 20 color (0, 0, 0) depth 1 filled fillColor (230, 230, 230)
  addCodeLine "allTowers = {" to "sourceCode" indentation 2
  highlightNode on "Graph" nodes 0 2 3 after 100 ticks  within 100 ticks
  highlightEdge on "Graph" (0,1) (0,2) (0,3)
  group "grp" "polyline" "Circle11" "Graph"
  move "field" "Graph" "square" corner SE to (220, 580)
}
{
  fillColor "triangle" blue
  color "box" type "color" yellow
  move "triangle" "Texti" via "polyline" within 3000 ms
  move "array" "Circle11" "box" corner NE along line (70, 90) (120, 140) (170, 90) within 3000 ms
  unhighlightEdge on "Graph" (0,1)
  unhighlightNode on "Graph" nodes 0 2
  setEdgeWeight of "Graph" edge (0, 1) to "K"
  remove "grp" "Graph"
  hideCode "sourceCode"
}
{
  group "grp2" "Graph" "grp"
  arrayPointer "pointer" on "array" atIndex 2 label "2" long
  arrayIndex "index" on "array" atIndex 4 label "4" color blue 
  arrayMarker "marker" on "array" atIndex 5 label "5" short
}
{
  moveArrayMarker "pointer" to position 3
  moveArrayMarker "index" to arrayEnd
  jumpArrayMarker "marker" to outside
}
{
  highlightCode on "sourceCode" line 1 row 0  
}
{
  unhighlightCode on "sourceCode" line 1 row 0  
  highlightCode on "sourceCode" line 2 row 0 context
}
{
  arraySwap on "array" position 0 with 3
}
{
  arrayPut "kissa" on "array" position 2
}
{
  highlightArrayCell on "array" position 3
  highlightArrayElem on "array" position 1
}
{
  unhighlightArrayCell on "array"
}
{
  highlightArrayCell on "array" from 2 to 4
}
{
  unhighlightArrayCell on "array"
}
{
  highlightArrayCell on "array" to 1
}
{
  unhighlightArrayCell on "array"
}
{
  highlightArrayCell on "array" from 2
}
{
  unhighlightArrayCell on "array"
}
{
  highlightArrayCell on "array"
}
